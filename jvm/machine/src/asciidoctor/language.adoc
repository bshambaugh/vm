:imagesdir: ./images/language
:subs: non

== Language Structures

image::compiling-languages.png[float="right",width=300]

The mm-ADT VM is written in https://scala-lang.org/[Scala]. It contains a fluent program builder https://en.wikipedia.org/wiki/Application_programming_interface[API] called `mmscala`. Most compiler designers should avoid `mmscala` because direct access to the https://en.wikipedia.org/wiki/Java_virtual_machine[JVM] and the full JDK can create unsafe mm-ADT programs. Instead, `mmlang` should be the target language for most compilers. Moreover, all compilers targeting `mmlang` can be used across different mm-ADT VM implementations.

IMPORTANT: The recommended target language for all higher-level language compilers is `mmlang`.

.Products and Coproducts
****
https://en.wikipedia.org/wiki/Category_theory[Category theory] is the study of structure via manipulations that expose (or generate) other structures. Two important category theoretic concepts used throughout this section are *products* and *coproducts*.

image::product.png[float="left",width=225]

A https://en.wikipedia.org/wiki/Product_(category_theory)[product] is any object defined in terms of it's accessible component objects. That is, from a single object, via \$\pi_n\$ projection morphisms, the product is decomposed into it's constituent parts.

image::coproduct.png[float="right",width=225]

A https://en.wikipedia.org/wiki/Coproduct[coproduct] is any object defined in terms the component objects used to construct it. That is, from many objects, via \$\iota_n\$ injection morphisms, a coproduct can be composed from constituent parts.

Along with these decomposition (and composition) morphisms, there exists an https://en.wikipedia.org/wiki/Isomorphism[isomorphism] between any two products (or coproducts) should they project (or inject) to the same component objects. That is, product and coproduct equality are defined via component equality.
****

=== The Obj

image::obj-type-value-q.png[role=left,padding-left=303,width=280]

Everything that can be denoted in `mmlang` is an `obj`. Within the VM and outside the referential purview of an interfacing language, every `obj` is the product of

. An *object* that is either a *type* or a *value* and
. A *quantifier* specifying the "amount" of objects being denoted.

\[
\texttt{obj} = (\texttt{type} + \texttt{value}) \times \texttt{q}.
\]

image::obj-type-q-value-q.png[float=right,width=450]

This internal structure is well-defined as an https://en.wikipedia.org/wiki/Ring_(mathematics)[algebraic ring]. The ring axioms specify how the internals of an `obj` are related via two binary operators: \$\times\$ and \$\+\$ . One particular axiom states that products both left and right https://en.wikipedia.org/wiki/Product_(category_theory)#Distributivity[distribute] over coproducts. Thus, the previous formula can be rewritten as \[
\texttt{obj} = (\texttt{type} \times \texttt{q}) + (\texttt{value} \times \texttt{q}).
\]
This representation, which is https://en.wikipedia.org/wiki/Isomorphism[isomorphic] to the previous, states two distinct kinds of mm-ADT `objs`: *quantified types* and *quantified values*. This is the *obj meta-model*.

[source]
----
mmlang> int            <1>
==>int
mmlang> 1              <2>
==>1
mmlang> int{5}         <3>
==>int{5}
mmlang> 1{5}           <4>
==>1{5}
mmlang> 'a','b','a'    <5>
==>'a'{2}
==>'b'
----
<1> A single `int` type.
<2> A single `int` value of `1`.
<3> Five `int` types.
<4> Five `1` `int` values.
<5> A _stream_ of `'a'`,`'b'`, and `'a'` `str` values.

==== Types and Values

Many modern programming environments make a sharp distinction between types and values, where each is predominately the focus of either https://en.wikipedia.org/wiki/Compiler[compilation] (types) or evaluation (values). In mm-ADT, instructions operate on both types and values. Two interesting consequences are that compilation is simply running an mm-ADT program with type arguments and when values are applied to types during evaluation, types serve as https://en.wikipedia.org/wiki/Anonymous_function[lambda (or anonymous) functions].

[source]
----   
mmlang> int int[is>0]                 <1>
==>int{?}<=int[is,bool<=int[gt,0]]
mmlang> 5 int[is>0]                   <2>
==>5
mmlang> -1 int[is>0]                  <3>
mmlang>
----
<1> The `int` type is passed to the `int[is>0]`-type to yield an either 0 or 1 `int` (compilation)
<2> The type `[is>0]` serves as a lambda of the form \[ \lambda x = \begin{cases} x & \text{if } x > 0 \\ \emptyset & \text{otherwise.} \end{cases} \]
<3> The type `[is>0]` filtering the `int` value -1.


[width=60]
|===
| Types | Values

| Refer to themselves and values | Refer to themselves
| Generalized values | Specified types
| A non-unit function | A unit function
|===

The continuum between types and values eases many longstanding distinctions in computer science. The table below presents a collection of the more interesting conceptual blurs that are introduced in mm-ADT. The particulars of each will be discussed over the course of this documentation.

.Consequences of Type/Value Unification
[cols="2,2,10"]
|===
|structure A | structure B | unification

|types       | values     | quantifiers expand the cardinality of values and constrain the cardinality of types
|compilation | evaluation | compilations are evaluations with types, where a compilation error is a "runtime compile-time" error.
|AST         | bytecode   | the algebra of type construction relaxes the necessity for different program encodings.
|state       | values     | 'mutable' state are values from historic types contained within the structure of the current type.
|functions   | types      | functions are (dependent) types with value references generated at evaluation.
|programs    | types      | a program is a "complicated" type.
|classical   | quantum    | quantum computing is classical computing with unitary matrix quantifiers.
|canonical   | atomics    | atomic values and canonical types are both "tokens" by which all constructions are built.
|===

=== The Type

An `obj` was previously defined as \[
\texttt{obj} = (\texttt{type} \times \texttt{q}) + (\texttt{value} \times \texttt{q}).
\]

This definition specifies no internal structure to a type or value. If this definition was complete, then types and values would be isomorphic and thus, indistinguishable. This is not the case. Types are defined in terms of other types. Values are not. mm-ADT types are a coproduct, where every type is either a

. *canonical type* (ctype): a base/fundamental type, or a
. *derived type* (dtype): a product of a type and an instruction (`inst`).

The ctypes are https://en.wikipedia.org/wiki/Nominal_type_system[nominal types]. There are five ctypes:

image::type-product.png[float=right,width=295]

. *bool*: denotes the set of booleans -- \$ \mathbb{B} \$.
. *int*: denotes the set of integers -- \$ \mathbb{Z} \$.
. *real*: denotes the set of reals -- \$ \mathbb{R} \$.
. *str*: denotes the set of character strings -- \$ \Sigma^\ast \$.
. *poly*: denotes the set of composites (polynomials) -- \$ \tt{obj}^n \$.

The dtypes are https://en.wikipedia.org/wiki/Structural_type_system[structural types] whose https://en.wikipedia.org/wiki/Recursive_data_type[recursive definition] is ultimately based at a ctype via a chain of instructions (`inst`) that operate on types to yield types. The path back to a type's base ctype is called the type's *trace*. Formally, the type coproduct is defined as

\[
  \begin{split}
    \texttt{type} &=\;& (\texttt{bool} + \texttt{int} + \texttt{real} + \texttt{str} + \texttt{poly}) + (\texttt{type} \times \texttt{inst}) \\
    \texttt{type} &=\;& \texttt{ctype} + (\texttt{type} \times \texttt{inst}) \\
    \texttt{type} &=\;& \texttt{ctype} + \texttt{dtype}, 
  \end{split}
\]

where each component of the coproduct also has a respective quantifier as previously defined for all `objs`.

A dtype has two \$pi\$-projections. The type projection denotes the _domain_ and the instruction projection denotes the _function_, where the type product as a whole, relative to the component projections, is the _range_.
\[
  \begin{split}
    \tt{type} &=\;& (\tt{type} &\;\times\;& \tt{inst}) &\;+\;& \tt{ctype} \\ 
    \text{â€œrange} &=\;& (\text{domain} &\;\text{and}\;& \text{function}) &\;\text{or}\;& \text{base"} 
  \end{split}
\]

The implication of the dtype product is that mm-ADT types are generated https://en.wikipedia.org/wiki/Inductive_type[inductively] by applying instructions from the mm-ADT VM's https://en.wikipedia.org/wiki/Instruction_set_architecture[instruction set architecture] (`inst`). The application of an `inst` to a type (ctype or dtype) yields a dtype that is a structural expansion of the previous type.

image::ctype-dtype.png[align=center,width=230]

For example, `int` is a ctype denoting a single `int` value from the set of all integers. When the instruction `[is>0]` is applied to `int`, the dtype `int[is>0]` is formed. This dtype is a https://en.wikipedia.org/wiki/Refinement_type[refinement type] that restricts `int` to only those `int` values greater than zero -- i.e., a natural number \$\mathbb{N}^+\$. In terms of the "__range = domain and function__" reading, when `int` (*domain*) is applied to `[is>0]` (*function*), the result is an `int` greater than zero (*range*).

image::int_isgt0.png[align=center,width=260]

The diagram above is a representation of a structure that is important across numerous processes in mm-ADT including type checking, type inference, compiler optimization, garbage collection, and more. It has a name. It's called the program's *type graph*.

image::obj-full.png[float=right,width=320]

The full `obj` structure thus far is diagrammed on the right and some type construction examples are presented in `mmlang` on the left.

[source]
----
mmlang> int                                      <1>
==>int
mmlang> int{2}                                   <2>
==>int{2}
mmlang> int{2}[is>0]                             <3>
==>int{0,2}<=int{2}[is,bool{2}<=int{2}[gt,0]]
----
<1> A ctype denoting a single integer stream.
<2> A ctype denoting a stream with two integers.
<3> A dtype denoting a stream of zero, one, or two integers.

The salient features of a type are captured in the psuedo-`mmlang` expression below, where the three projections are conviently understood as encoding a type's

. *Type signature*: the ctype specification of a type's domain and range (via the \$ \pi_\tt{domai\n} \$ and \$ \pi_\tt{rang\e} \$ projections), and
. *Type definition*: an instruction tree specifying domain `obj` processes (via the \$ \pi_\tt{i\nsts} \$ projection).

image::type-signature-definition.png[align=center,width=650]

==== Type Signatures

Every mm-ADT type can be generally understood as a function that maps an `obj` of one type to an `obj` of another type. A *type signature* specifies the source and target of this mapping, where the _domain_ is the source type and the _range_ is the target type. Both the domain and range type specifications include a respective quantifier denoted `{q}` in `mmlang`. The general pattern of a type signature is

[source,align=center]
----
range{q}<=domain{q}
----

[cols="5,10"]
|===
|mmlang Expression |Description

a|
[source]
----
mmlang> int{1}
==>int
----
|In most programming languages, a value can be typed `int` as in `val x:int = 10`. Such https://en.wikipedia.org/wiki/Declaration_(computer_programming)[declarations] state that the value referred to by `x` is a _single_ element within the set of integers. The concept of a "single element" is captured in mm-ADT by the `obj` quantifier, where a https://en.wikipedia.org/wiki/Unit_(ring_theory)[unit] quantifier is not displayed in `mmlang`.

a|
[source]
----
mmlang> int<=int
==>int
----
|From the perspective of type-as-function, An mm-ADT `int` is a https://en.wikipedia.org/wiki/NOP_(code)[no-op] on the set of integers. Given any integer, `int` returns that integer. In `mmlang`, when the domain and range are the same, the `\<=` and repeated type are not displayed.

a|
[source]
----
mmlang> int<=bool
error: bool is not an int
----
|Type's that are fully specified by their type signature are always canonical types. Without any instructions, there is no way that this (meaningless) type can map a `bool` to an `int`. 

a|
[source]
----
mmlang> int{5}
==>int{5}
----
|`int{5}` is a type referring to 5 integers (with repeats possible according to stream semantics). As a point of comparison, `int` denotes a stream containing a single integer. This is why `int` is syntactic sugar for `int{1}` in `mmlang`.

a|
[source]
----
mmlang> int{0,5}
==>int{0,5}
mmlang> int{0,5}<=int{0,5}
==>int{0,5}
----
|Quantifiers must be elements from a ring with unity. In the previous examples, the chosen quantifier was the integer ring \$\langle \mathbb{Z},+,\times \rangle\$. In this example, the https://en.wikipedia.org/wiki/Algebraic_structure[carrier set] is two integers and represents uncertainty as to the number of elements being referred to. `int{0,5}` is a type referring to either 0, 1, 2, 3, 4, or 5 integers.
|===

==== Type Definitions

Types and values both have a *ground* that exists outside of the mm-ADT VM within the hosting environment (e.g. the JVM). The ground of the mm-ADT value `2` is the JVM primitive `2L` (a Java `long`). The ground of the mm-ADT type `int` is the JVM class `java.lang.Long`. When the instruction `[plus,4]` is applied to the mm-ADT `int` value `2`, a new mm-ADT `int` value is created whose ground is the JVM value `6L`. When `[plus,4]` is applied to the mm-ADT `int` type, a new type is created with the same `java.lang.Long` ground. Thus, the information that distinguishes `int` from `int[plus,4]` is in the remembrance of the instruction that was applied to `int`. For a type, this history is called the *type definition* (and more generally, the *type's trace*).

NOTE: In practice, the string representation of a value is it's ground and the string representation of a type is it's trace.

image::type-value-trace.png[align=center,width=550]

Both types and values exist in a larger graph called the *obj trace graph*. In theory, the complete history of an mm-ADT program from compilation to execution is stored in this graph. However, in practice, the mm-ADT VM removes those traces (particular paths through the graph) that will no longer be needed by mm-ADT program. This process is called *trace removal* and is the mm-ADT equivalent of https://en.wikipedia.org/wiki/Garbage_collection_(computer_science)[garbage collection].

==== Type Quantification

.Quantifier Symbols in mmlang
[width=35%,cols="1,1,2,float=right]
|===
|usage   |sugar  | unsugared

|https://en.wikipedia.org/wiki/Option_type[none]/nothing   |`{0}`  | `{0,0}`
|https://en.wikipedia.org/wiki/Option_type[some]/just      |       | `{1,1}`
|exact          |`{x}`  | `{x,x}`
|least footnote:order[Applicable to quantifier rings with an total order over the carrier.]         |`{x,}` | `{x,max}`
|most footnote:order[]          |`{,x}` | `{min,x}`
|https://en.wikipedia.org/wiki/Option_type[option]/maybe   |`{?}`  | `{0,1}`
|given footnote:order[]         |`{+}`  | `{1,max}`
|any footnote:order[]           |`{*}`  | `{1,max}`
|===

In order to quantify the _amount_ of values denoted by a type, every mm-ADT type has an associated quantifier `q`.
Quantifiers are typically integers, but can be any element from an ordered algebraic ring (e.g. integers, reals in \$ \mathbb{R}, \mathbb{R}^2, \mathbb{R}^3, \ldots, \mathbb{R}^n \$, unitary matrices, etc.).
Moreover, while integer quantifiers signify "amount," other quantifiers such as unitary matrices used to represent a https://en.wikipedia.org/wiki/Wave_function[quantum wave function], "amount" is a less accurate description.

Common programming concepts that are usually captured by typeclasses or functors (via https://en.wikipedia.org/wiki/Lift_(mathematics)[lifting]) are expressed in mm-ADT via quantification.

[source]
----
int                               // a single int   (some)
int{?}<=int[is,[gt,0]]            // 0 or 1 int     (option)
int{0}<=int[is,false]             // 0 ints         (none)
int{4}<=int{2}[branch,[id],[id]]  // 4 ints         (exact)
int{*}<=[=db][get,'people']       // 0 or more ints (any)
----

NOTE: The default quantifier ring of the mm-ADT VM is \[
\langle \mathbb{N}^+ \times \mathbb{N}^+, \ast, + \rangle, \]
where \$(1,1)\$ is the multiplicative identity (unity) and \$(0,0)\$ is the additive identity. The \$\ast\$ and \$ +\$ binary operators are pairwise integer multiplication and addition, respectively. In `mmlang` if an `obj` quantifier is not displayed, then the quantifier is assumed to be the unity of the ring, or `{1,1}`. Moreover, if a single value is provided, it is assumed to be repeated where `{n}` is shorthand for `{n,n}`.

==== Type Compilation

Every instruction is able to operate on both types and values. Instructions applied to types is program _compilation_ and instructions applied to values is program _evaluation_.

[cols="10,10"]
|===
|mmlang Expression |Description

a|
[source]
----
mmlang> int[is,[gte,0]]
==>int{?}<=int[is,bool<=int[gte,0]]
----
|A base type composed with an instruction/quantifier pair, where the quantifier is not displayed as it's the unit of the quantifier ring -- `{1}`. The result is a compiled type that maps a single integer to zero or one integers. If the internal `bool\<=int[gte,0]` type yields `true`, then an `int{1}`, else `int{0}`.

a|
[source]
----
mmlang> int int[is,[gte,0]]
==>int{?}<=int[as,int][is,bool<=int[gte,0]]
----
|A type operates on an `int` thus, if supplied "an `int` type", the type operates on it to yield a type. The use of types to process types is a form of compilation generally known as https://en.wikipedia.org/wiki/Abstract_interpretation[abstract interpretation].

a|
[source]
----
mmlang> 6 int{?}<=int[is,bool<=int[gte,0]]
==>6
----
|The type maps the positive integer `6{1}` to `6{1}` (_some_).

a|
[source]
----
mmlang> -6 int{?}<=int[is,bool<=int[gte,0]]
mmlang>
----
|The type maps the negative integer `-6{1}` to `-6{0}` (_none_).
|===

==== The Type Trace Graph

Every mm-ADT program is a _type_. Every type is created by a https://en.wikipedia.org/wiki/Type_constructor[type constructor]. The ctypes are degenerate _nullary_ constructors. The dtypes are `n`-ary constructors with their arity being the number of instructions applied to their respective base ctype. The arity is also the path length of the spine of the type's definition within a https://en.wikipedia.org/wiki/Directed_graph[directed labeled graph] called a *type trace graph*.

===== Linear Type Traces

Any type constructed with instructions contain argument values (not types) generates a *linear type trace*.

[source]
----
mmlang> int
==>int
mmlang> int[id]
==>int[id]
mmlang> int[id][gt,0]
==>bool<=int[id][gt,0]
----

===== Nested Type Traces

Any type constructed with instructions that contain an argument type generates a *nested type trace*.

[source]
----
mmlang> int
==>int
mmlang> int[id]
==>int[id]
mmlang> int[id][is,[gt,0]]
==>int{?}<=int[id][is,bool<=int[gt,0]]
mmlang>
----

===== Branching Type Traces

Nested types are examples of _inter-instruction branching_. The more general form is _intra-instruction branching_. Any instruction that forks an `obj` into a `poly` or joins the `objs` of a `poly` are *branch instructions*. They generate *branching type traces*.

A https://en.wikipedia.org/wiki/Tagged_union[*sum type*] (tagged union) is constructed by branching across a `|-poly`.

image::sum-type-constructor.png[width=600,align=center]

[source]
----
mmlang> int~<[int[is>0] | int[neg]]
==>[int{?}<=int[is,bool<=int[gt,0]]|int[neg]]<=int~<[int{?}<=int[is,bool<=int[gt,0]]|int[neg]]
----

In the example above, a `int` is the ctype that is split across two branches via `[choice]` (with a sugar of `~<`). Given an `int` value, that `int` value will either be greater than 0 or it will be negated.  In other words, the type maps an `int` to a coproduct containing it's https://en.wikipedia.org/wiki/Absolute_value[absolute value] as determined either from the first or the second branch.

[source]
----
mmlang> 10<int[is>0] | int[neg]>
==>[10|]
mmlang> -10<int[is>0] | int[neg]>
==>[|10]
----

While both injections of the coproduct contain the same value, their location in the coproduct contains information about the state of the domain `int`. For example, should the coproduct undergo a `[merge]` (with a sugar of `>-`), then the above type yields the same range value for two different `int` domains.

[source]
----
mmlang> 10<int[is>0] | int[neg]>
==>10
mmlang> -10<int[is>0] | int[neg]>
==>10
----

Without the merge, the branch that the incoming `int` took is preserved in the coproduct's `poly` structure and can be inverted.

[source]
----
mmlang> 10<int[is>0] | int[neg]]=[_|[neg]>
==>10
mmlang> -10<int[is>0] | int[neg]]=[_|[neg]>
==>-10
----

=== Language Algebras

==== The Polys

Every composite structure, including products and coproducts, are an mm-ADT `poly`. There are two kinds of `polys`:

. `lst`: a list indexed by `int` whose elements are `objs`.
. `rec`: a map indexed by arbitrary `obj` keys, whose values are `objs`.

Every `poly` has a separator. The separator provides details the way in which the poly should be processed. There are three kinds of separators.

[cols="2,1,2,3"]
|===
|structure               |sep   |name               |quantifier

|coproduct w/ eliminator | `\|` | choice or either  | `{n}\|{m}` quantified as \$n\|m\$
|coproduct               | `,`  | all or parallel   | `{n},{m}` quantified as \$n+m\$
|product                 | `;`  | compose or serial | `{n};{m}` quantified as \$n \ast m\$.
|===

[source]
----
mmlang> int<[mult,2]{10}|[plus,1]{20}>
==>int{10,20}<=int<[int{10}<=int[mult,2]{10}|int{20}<=int[plus,1]{20}]>
mmlang> int<[mult,2]{10},[plus,1]{20}>
==>int{30}<=int<[int{10}<=int[mult,2]{10},int{20}<=int[plus,1]{20}]>
mmlang> int<[mult,2]{10};[plus,1]{20}>
==>int{200}<=int<[int{10}<=int[mult,2]{10};int{200}<=int{10}[plus,1]{20}]>
----

A few examples are provided.

[cols="2,4"]
|===
| mmlang                              | description

| `<int\|str>`                        | either an `int` or a `str`.
| `<int[is>20]\|int[is<10]]>`         | either an `int` greater than 20 or an `int` less than 10.
| `<int[is>20]<x>\|int[is<10]<y>]>`   | either an `int` greater than 20 labeled `x` or an `int` less than 10 labeled `y`.
| `<int+1,int+2>`                     | both `int` incremented by `1` and incremented by `2`.
| `<int+1;int+2>`                     | an `int` incremented by `1` and then incremented by `2`.
|===

[source]
----
mmlang> 6<int|str>
==>6
mmlang> 6<int+1|int+2>
==>7
mmlang> 6<int+1,int+2>
==>7
==>8
mmlang> 6<int+1;int+2>
==>9
----

===== A Poly of Streams or a Stream of Polys

[source]
----
mmlang> [1,2,3,4,5]>--<[int+1,int*2]
==>[3,4]
==>[2,2]
==>[4,6]
==>[5,8]
==>[6,10]
----

[source]
----
mmlang> 1-<[+2,<+3,+4>]
==>[3,4,5]
----

==== Bicartesian Closed Category

.Initial and Terminal Objects
****
image::initial.png[width=130,float=left]

A category may have an https://en.wikipedia.org/wiki/Initial_and_terminal_objects[initial and/or terminal] object.

An *initial object* \$S\$ is the domain of a set of morphism \$S \rightarrow E_n\$. Initial objects, via their morphisms, generate all the objects of the category. If there is an initial object, then it is unique in that if there is another initial object, it has the same diagrammatic topology -- all outgoing morphisms and no incoming morphisms save the identity. Thus, besides labels, two initials are isomorphic.

image::terminal.png[width=130,float=right]

A *terminal object* \$E\$ is the range of a set of morphisms \$S_n \rightarrow E\$. Terminal objects subsume all other objects in the category in that all other objects \$S_n\$ can be morphed into the terminal object, but the terminal object can not be morphed into any other object. Similar to initials, should another terminal exist, the two terminal are isomorphic in that they both have the same number of incoming morphisms and no outgoing morphisms (save the identity).

&nbsp;
****

A https://en.wikipedia.org/wiki/Cartesian_closed_category#Bicartesian_closed_categories[bicartesian closed category] has products, coproducts, a product unit, a coproduct unit, exponents, and products distribute over coproducts. These features are captured by the following axioms that all BC categories must support.

[cols="2,3,4"]
|===
| axiom | mmlang type | description

| \$a + b = b + a \$                           | `<a\|b> = <b\|a>`                    | commutative coproducts
| \$(a + b) + c = a + (b + c)\$                | `\<<a\|b>\|c> = <a\|<b\|c>>`         | associative coproducts
| \$a \ast (b + c) = (a \ast b) + (a \ast c)\$ | `<a;<b\|c>> = \<<a;b>\|<a;c>>`       | distributivity
| \$0 + a = a\$                                | `<_{0}\|a> = a`                      | coproduct unit
| \$0 \ast a = 0\$                             | `<_{0};a> = _{0}`                    | coproduct unit annihilation
| \$1 \ast a = a\$                             | `<_{1};a> = a`                       | product unit
| \$c^ {b^a} = c^{a \ast b}\$                  | `<a;b;c> = <<a;b>;c>`                | currying
|===

==== The Inst Monoid

.Monoids
****
A https://en.wikipedia.org/wiki/Monoid[monoid] is a structure of the form \$\langle A,\ast \rangle\$, where \$A\$ is the carrier set closed under the associative binary operator \$\ast: A \times A \rightarrow A\$ with \$1 \in A\$ being the https://en.wikipedia.org/wiki/Identity_element[identity] such that for every \$a,b,c \in A\$, \$(a \ast b) \ast c = a \ast (b \ast c)\$ and \$a \ast 1 = 1 \ast a = a\$.
****

mm-ADT types serve numerous roles which are typically realized by many different sorts of objects in traditional programmable systems. The reason for this singular use is quite literally because the mm-ADT type system (and value system) is inductively generated from the mm-ADT VM's https://en.wikipedia.org/wiki/Instruction_set_architecture[instruction set architecture] which is the https://en.wikipedia.org/wiki/Generator_(mathematics)[generator set] of the _syntactic_ **inst monoid** \[
\langle\texttt{inst},\ast\rangle, \]
where \$\tt{i\nst}\$ is the infinite set of all instruction compositions, \$\ast: \tt{i\nst} \times \tt{i\nst} \rightarrow \tt{i\nst}\$ an associative binary composition operator, and \$\tt{[no\op]} \in \tt{i\nst}\$ is the unique identity element. This "instruction-only" interpretation of mm-ADT realizes the general unitary form of mm-ADT \$ f: S \rightarrow E \$ where `S` is `obj{0}` (i.e. nothing). The `x\<=[start,x]` instruction is an *initial instruction* in that it returns it's arguments regardless of the input `obj` (or lack thereof).

\[
\texttt{[start,x]}: \texttt{obj\{0\}} \rightarrow \texttt{x}
\]

An mm-ADT program is a _type_, not a composition of instructions (\$ \tt{type} = (\tt{type} \times \tt{i\nst})\$). The way in which a dtype can be ground to a ctype using instructions only is via the `[start]` instruction. A single pass through the mm-ADT compiler yields a type trace based at a ctype.

image::start-ctypes.png[float="right",width=315]

[source]
----
mmlang> [start,int]
==>int
mmlang> [start,int][plus,5]
==>int[plus,5]
mmlang> [start,int][plus,5][gt,10]
==>bool<=int[plus,5][gt,10]
mmlang>
----

The `[noop]` instruction is the only mm-ADT instruction that does not alter the state of the `obj` trace graph and thus, the state of the computation. The instruction `[id]`, on the other hand, does.

[source]
----
mmlang> [start,int][plus,5][gt,10][noop]
==>bool<=int[plus,5][gt,10]
mmlang> [start,int][plus,5][gt,10][id]
==>bool<=int[plus,5][gt,10][id]
----

There exists an isomorphism (a https://en.wikipedia.org/wiki/Full_and_faithful_functors[bijective functor]) that maps the `inst` syntactic monoid to the type trace graph. This bijection proves that the syntax of `mmlang` is sufficient to express every possible mm-ADT type and vice versa, every mm-ADT type has a corresponding expression in `mmlang`.

image::inst-type-functor.png[width=35%,align=center]

The `inst`-monoid is an algebraic structure that can be used by all untyped languages wishing to compile to the mm-ADT VM. It can be used to write expressions, though, given the lack of typing, not all expressions will properly compile. The benefit of the `inst`-monoid is the simplicity of development, with the drawback of offloading the language-level type checking to the user.

==== The Type Ringoid

.Stream Ring Theory
****
https://zenodo.org/record/2565243[Stream ring theory] is a ring algebra defined by the https://en.wikipedia.org/wiki/Product_ring[direct product] of a *function* and *coefficient* ring, where every function/coefficient pair is an element of the carrier of a polynomial *stream* ring. The algebra is useful in asynchronous distributed computing environments that primarily enjoy https://en.wikipedia.org/wiki/Embarrassingly_parallel[embarrassingly parallel] processing, but where, at certain space and time https://en.wikipedia.org/wiki/Bulk_synchronous_parallel[synchronization] points, large amounts of data need to be co-located for processing. A proto-version of the stream ring algebra was realized in the distributed graph computing framework http://tinkerpop.apache.org[Apache TinkerPop].

mm-ADT adopts the algebra of stream ring theory, where functions are *instructions* and coefficients are *quantifiers*. mm-ADT's type system is realized as a https://en.wikipedia.org/wiki/Many-sorted_logic[multi-sorted] extension of the algebra.
****

mm-ADT's type system is founded on the mathematics of multi-sorted https://en.wikipedia.org/wiki/Ring_(mathematics)[rings] with https://en.wikipedia.org/wiki/Unit_(ring_theory)[unity] -- the mm-ADT *type ringoid*. A ring relates two monoids that share a carrier set. The mm-ADT type ringoid https://en.wikipedia.org/wiki/Generator_(mathematics)[generator] is the set of all instructions (`inst`), where, with the ringoid's multiplicative operator (`*`) and commutative additive operator (`+`) the set of all possible mm-ADT types is induced.

* *Addition* is encoded using a `,-poly`, where each slot of the polynomial is an independent parallel type.
* *Multiplication* is encoded using a `;-poly`, where each slot of the polynomial is a dependent serial type.
* The *additive identity* `0` is `[id]{0}`. Given all `{0}` quantified `objs` are isomorphic any `{0}` can be used in stead.
* The *multiplicative identity* `1` is `[id]{1}`, which in `mmlang` is represented as `[id]`.

NOTE: The algebra underlying most type theories operate as a https://en.wikipedia.org/wiki/Semiring[semiring], where the additive component is a https://en.wikipedia.org/wiki/Monoid[monoid] as opposed to an invertible https://en.wikipedia.org/wiki/Group_(mathematics)[group]. In mm-ADT, the additive structure support inversion with _negative types_ that entail a ring with an additive group, where the `,-poly` `[a{1},a{-1}]` merges to `a{0}` or `{0}`.

The ring https://en.wikipedia.org/wiki/Axiom[axioms] are presented below along with the respective mm-ADT structure denoted in `mmlang`.

.Ring with Unity Axioms
[cols="^4,^4,^6"]
|===
| common notation                  | terminology                              | mmlang notation

|`(a + b) + c = a + (b + c)`       | additive associativity                   | `<<[a],[b]>,[c]>` \$=\$ `<[a],<[b],[c]>>`
|`0 + a = a + 0 = a`               | additive identity                        | `<[id]{0},[a]>` \$=\$ `<[a],[id]{0}>` \$=\$ `[a]`
|`a - a = a + (-a) = 0`            | additive inverses                        | `<[a],[a]{-1}>` \$=\$ `[id]{0}`
|`a + b = b + a`                   | additive commutativity                   | `<[a],[b]>` \$=\$ `<[b],[a]>`
|`(a * b) * c = a * (b * c)`       | multiplicative associativity             | `<<[a];[b]>;[c]>` \$=\$ `<[a];<[b];[c]>>`
|`1 * a = a * 1 = a`               | multiplicative identity                  | `<[id];[a]>` \$=\$ `<[a];[id]>` \$=\$ `[a]`
|`(a + b) * c = (a * c) + (b * c)` | multiplicative left distributivity       | `<<[a],[b]>;[c]>` \$=\$ `<<[a];[c]>,<[b];[c]>>`
|`a * (b + c) = (a * b) + (a * c)` | multiplicative right distributivity      | `<[a];<[b],[c]>>` \$=\$ `<<[a];[b]>,<[a];[c]>>`
|===

'''

====

image::delta-nabla.png[width=650,align=center]

mm-ADT composes unary functions via `*` and `+`. `n`-ary functions are encoded as `poly`-coupled unary functions with the \$\Delta\$-projection (delta) serving to **copy** (clone via `,`) or **split** (partition via `|`) a single `obj`.

\[
\begin{split}
\Delta_n&: A &\rightarrow \overbrace{A \times \ldots \times A}^{n-\text{times}} \\
\Delta_n&(a) &= [a,\ldots,a]
\end{split}
\]

.Copy \$\Delta\$
[source]
----
mmlang> 'a'-<[_,_]
==>['a','a']
mmlang> 'a'-<[_+'b',_+'c']
==>['ab','ac']
----

.Split \$\Delta\$
[source]
----
mmlang> 'a'-<[_|_]
==>['a'|]
mmlang> 'a'-<[_+'b'|_+'c']
==>['ab'|]
----

Upon exiting an instruction, all internal `poly`-branches are serialized via a \$\nabla\$-injection (nabla) serves to  *merge* (union via `,`) or *select* (disjoint union via `|`) from many independent streams to a single stream.

\[
\begin{split}
\nabla_n&: \overbrace{B \times \ldots \times B}^{n-\text{times}} &\rightarrow B \\
\nabla_n&([b,\ldots,b]) &= b
\end{split}
\]

.Merge \$\nabla\$
[source]
----
mmlang> 'a'-<[_,_]>-
==>'a'{2}
mmlang> 'a'-<[_+'b',_+'c']>-
==>'ab'
==>'ac'
----

.Select \$\nabla\$
[source]
----
mmlang> 'a'-<[_|_]>-
==>'a'
mmlang> 'a'-<[_+'b'|_+'c']>-
==>'ab'
----
====

:table-caption: Table 1
.Ring with Unity Theorems
[cols="^",width=35%,float=right]
|===
|`a + b = a + c \=> b = c`
|`a + b = 0 \=> a = -b & b = -a`
|`-(a+b) = (-a) + (-b)`
|`-(-a) = a`
|`a*0 = 0 = 0*a`
|`a * (-b) = -a * b = -(a * b)`
|`(-a) * (-b) = a * b`
|===
The table on the right provides the (most well-known) logical entailments of said axioms -- i.e., https://en.wikipedia.org/wiki/Theorem[theorems]. Example mm-ADT types demonstrating the equalities of the ring axioms are presented below in `mmlang`, where

* `[a]` is `int[plus,1]`
* `[b]` is `int[mult,2]`
* `[c]` is `int[mult,3]`.

Though any combinations of respectively sorted instructions can be used.

'''

.The Ring Axioms in mmlang
[cols="^3,2,5"]
|===
|Ring Axiom |mm-ADT Diagram |mmlang Denotation

|`(a + b) + c = a + (b + c)`
|image:additive-associativity.png[link=images/language/additive-associativity.png]
a|
[.small]
[source]
----
mmlang> 1<<[plus,1],[mult,2]>,[mult,3]>
==>2{2}
==>3
mmlang> 1<[plus,1],<[mult,2],[mult,3]>>
==>2{2}
==>3
----

|`0 + a = a + 0 = a`
|image:additive-identity.png[link=images/language/additive-identity.png]
a|
[.small]
[source]
----
mmlang> 1<[id]{0},[plus,1]>
==>2
mmlang> 1<[plus,1],[id]{0}>
==>2
----


|`a - a = a + (-a) = 0`
|image:additive-inverse.png[link=images/language/additive-inverse.png]
a|
[.small]
[source]
----
mmlang> 1<<[plus,1],[mult,2]>,[mult,3]>
==>2{2}
==>3
mmlang> 1<[plus,1],<[mult,2],[mult,3]>>
==>2{2}
==>3
----

|`a + b = b + a`
|image:additive-commutativity.png[link=images/language/additive-commutativity.png]
a|
[.small]
[source]
----
mmlang> 1<[plus,1],[mult,2]>
==>2{2}
mmlang> 1<[mult,2],[plus,1]>
==>2{2}
----

|`(a * b) * c = a * (b * c)`
|image:multiplicative-associativity.png[link=images/language/multiplicative-associativity.png]
a|
[.small]
[source]
----
mmlang> 1<<[plus,1];[mult,2]>;[mult,3]>
==>12
mmlang> 1<[plus,1];<[mult,2];[mult,3]>>
==>12
----

|`1 * a = a * 1 = a`
|image:multiplicative-identity.png[link=images/language/multiplicative-identity.png]
a|
[.small]
[source]
----
mmlang> 1<[id];[plus,1]>
==>2
mmlang> 1<[plus,1];[id]>
==>2
mmlang> 1[plus,1]
==>2
----

|`(a + b) * c = (a * c) + (b * c)`
|image:multiplicative-right-distributivity.png[link=images/language/multiplicative-right-distributivity.png]
a|
[.small]
[source]
----
mmlang> 1<<[plus,1],[mult,2]>;[mult,3]>
==>6{2}
mmlang> 1<<[plus,1];[mult,3]>,<[mult,2];[mult,3]>>
==>6{2}
----

|`a * (b + c) = (a * b) + (a * c)`
|image:multiplicative-left-distributivity.png[link=images/language/multiplicative-left-distributivity.png]
a|
[.small]
[source]
----
mmlang> 1<[plus,1];<[mult,2],[mult,3]>>
==>4
==>6
mmlang> 1<<[plus,1];[mult,2]>,<[plus,1];[mult,3]>>
==>4
==>6
----
|===


Similarly, for the common ring theorems:

.The Ring Theorems in mmlang
[cols="^2,^3"]
|===
|Ring Theorems                        |mmlang Types

|`-(a+b) = (-a) + (-b)`               |`<[a],[b]>{-1}` \$=\$ `<[a]{-1},[b]{-1}>`
|`-(-a) = a`                          |`<[a]{-1}>{-1}` \$=\$ `[a]`
|`a*0 = 0 = 0*a`                      |`<[a];[id]{0}>` \$=\$ `[id]{0}` \$=\$ `<[id]{0};[a]>`
|`a * (-b) = -a * b = -(a * b)`       |`<[a];[b]{-1}>` \$=\$ `<[a]{-1};[b]>` \$=\$ `<[a];[b]>{-1}`
|`(-a) * (-b) = a * b`                |`<[a]{-1};[b]{-1}>` \$=\$ `<[a];[b]>`
|===

[width=45%,float=center]
|===
a|
[.small]
[source]
----
mmlang> 1<[plus,1],[mult,2]>{-1}
==>2{-2}
mmlang> 1<[plus,1]{-1},[mult,2]{-1}>
==>2{-2}
mmlang> 1<[plus,1]{-1}>{-1}
==>2
mmlang> 1[plus,1]
==>2
mmlang> 1<[plus,1];[id]{0}>
mmlang> 1[id]{0}
mmlang> 1<[id]{0};[plus,1]>
mmlang> 1<[plus,1];[mult,2]{-1}>
==>4{-1}
mmlang> 1<[plus,1]{-1};[mult,2]>
==>4{-1}
mmlang> 1<[plus,1];[mult,2]>{-1}
==>4{-1}
mmlang> 1<[plus,1]{-1};[mult,2]{-1}>
==>4
mmlang> 1<[plus,1];[mult,2]>
==>4
----
|===
'''

=== The Value

The mm-ADT type system is realized in real-world programs.

==== Uni Values

==== Poly Values